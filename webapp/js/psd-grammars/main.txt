corpus = tree+

obligws = [ \t\n]
ws = obligws*

tree = ws "( " tree:(idwithtree / leafastree) ")" ws { return tree; }
idwithtree = tree:nonterminal id:id? {
    var ret = { tree: tree };
    if (id) ret.id = id;
    return ret;
}
id = ws "(ID " id:[A-Za-z0-9.,-]+ ")" ws { return id.join(""); }
leafastree = leaf:leaf { return { tree: leaf }; }

nonterminal = ws "(" label:ntlabel index:index? obligws desc:(leaf / nonterminal)+ ")" ws {
    var ret = label;
    if (index) ret.index = index;
    ret.desc = desc;
    return ret;
}
ntlabel = cat:ntlabelpiece subcat:("-" ntlabelpiece)* {
var ret = {
    label: text()
};
    return ret;
}

ntlabelpiece = [A-Z][A-Z0-9]+ { return text(); }

leaf = ws leaf:( comment / noncommentleaf ) ws {
    return leaf;
}
noncommentleaf = "(" label:leaflabel index:index? " " rhs:leafrhs ")" {
    rhs.label = label;
    if (index) rhs.index = index;
    return rhs;
}
leafrhs = trace / ec / comment / text
trace = "*" tracetype:[A-Z]+ "*" index:index { return {
    type: "trace",
    index: index,
    tracetype: tracetype.join("")
}; }
ec = ec:(fullec / zeroec) { ec.type = "ec"; return ec; }
fullec = "*" ectype:[a-z]+ "*" { return { ectype: ectype.join("") }; }
zeroec = "0" { return { ectype: "zero" }; }

comment = "(CODE " com:(structuredcomment / freecomment) ")" {
    com.type = "comment"; com.label = "CODE"; return com;
}
structuredcomment = "{" comtype:[A-Z]+ ":" text:[^)}]+ "}" {
    return { comtype: comtype.join(""), text: text.join("") };
}
freecomment = text:[^)]+ { return { text: text.join("") }; }

index = idxtype:idxtype index:[0-9]+ { return { idxtype: idxtype, index: index.join("") }; }
idxtype = [-=] { return text === "=" ? "gap" : "regular" ; }
